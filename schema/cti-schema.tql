define
stix-object sub entity, 
	# Required
	owns stix-type,
	owns stix-id, # TODO should be a key
	owns custom-attribute,
	plays stix-core-relationship:active-role,
	plays stix-core-relationship:passive-role;

	stix-core-object sub stix-object, 
		# Required for SDO but Optional for SCO
		owns spec-version,

		# Optional
		owns extensions,

		plays object-marking:marked,
		plays creation:created,
		plays sighting:sighting-of,

		# Common relations for SDOs and SCOs. Defined in 3.7. 
		plays derivation:derived-from,
		plays derivation:deriving,
		plays duplicate:duplicated-object,
		plays relatedness:related-to;

			stix-domain-object sub stix-core-object, 
				# Required
				owns created,
				owns modified, 

				# Optional
				owns revoked, 
				owns labels,
				owns confidence,
				owns langs,
				plays external-referencing:referencing,

				plays indication:indicated;

			stix-cyber-observable-object sub stix-core-object,
				owns defanged,
				plays file-contain:contained-coo,
				plays analysis:captured-sco,
				plays reference:referred;

	stix-meta-object sub stix-object, 
		abstract, 
		owns created, 
		owns modified;

##### 2 Common Data Types #####

# 2.5 External Reference
external-reference sub stix-meta-object,
	# In addition to source-name, at least one of description, url or external-id must be present§
	# Required
	owns source-name, 

	# Optional
	owns description,
	owns url, 
	owns external-id,
	owns hash-general,
	plays external-referencing:referenced;

external-referencing sub relation, 
	relates referenced,
	relates referencing;

# 2.7 Hash
hash sub entity, 
	owns hash-value, 
	plays hash-association:contained-hash; 

	md5 sub hash;
	sha-1 sub hash; 
	sha-256 sub hash;
	sha-512 sub hash; 
	sha3-256 sub hash; 
	sha3-512 sub hash;
	ssdeep sub hash; 
	tlsh sub hash; 

# 2.11 Kill Chane Phase
kill-chain-usage sub stix-core-relationship,
	relates kill-chain-used,
	relates kill-chain-using;

kill-chain-phase sub stix-meta-object,
	owns kill-chain-name, 
	owns phase-name,
	plays kill-chain-usage:kill-chain-using,

	# inferred role player
	plays kill-chain:participating-kill-chain-phase;

##### 4 STIX™ Domain Objects #####

# Custom object
custom-object sub stix-domain-object,
	owns name, 
	owns description, 
	owns alias, 
	owns first-seen,
	owns last-seen,
	owns objective,
	plays kill-chain-usage:kill-chain-used,
	plays delivery:delivering,
	plays target:targetting,
	plays use:used-by,
	plays mitigation:mitigated,
	plays use:used;


# 4.1 
attack-pattern sub stix-domain-object,
	owns name,
	owns description,
	owns alias,

	# Relations defined as properties in STIX
	plays kill-chain-usage:kill-chain-used,

	# Common Relations
	plays delivery:delivering,
	plays target:targetting,
	plays use:used-by,

	# Reverse Relations
	# plays indication:indicated, -> Defined in SDO
	plays mitigation:mitigated,
	plays use:used; 

	# Embedded Relations
	# Creation
	# Object marking ref

# 4.2 Campaign
campaign sub stix-domain-object, 
	owns name, 
	owns description, 
	owns alias, 
	owns first-seen,
	owns last-seen,
	owns objective, 

	# Common Relations
	plays attribution:attributing,
	plays compromise:compromising,
	plays origination:originating,
	plays target:targetting,
	plays use:used-by;

	# Reverse Relations
	# plays indication:indicated; -> Defined in SDO

	# Embedded relations
	# Creation
	# Object marking

# 4.3 Course of Action
course-of-action sub stix-domain-object,
	owns name, 
	owns description, 
	owns action,

	# Common Relations
	plays investigation:investigating,
	plays mitigation:mitigating,
	plays remediation:remediating;

	# Reverse Relations
	# NA

	# Embedded relations
	# Creation
	# Object marking ref

# 4.4 Grouping 
grouping sub stix-domain-object,
	owns name, 
	owns description, 
	owns context,

	# Common Relations
	# NA 

	# Reverse Relations
	# NA

	# Embedded Relations
	# Creation
	# Object markings
	plays reference:referring;

# 4.5 Identity
identity sub stix-domain-object,
	owns name, 
	owns description, 
	owns stix-role,
	owns sector,  # should this perhaps be a relation?
	owns contact-information,

	# Common Relations
	plays located-at:locating,

	# Reverse Relations
	plays target:targetted,
	plays attribution:attributed, 
	plays impersonate:impersonated,

	# Embedded Relations
	plays creation:creator,
	# Object marking ref
	
	plays sighting:saw; 

	# 10.7 Identity Class Vocabulary
	individual sub identity;
	group sub identity;
	system sub identity;
	organization sub identity;
	class sub identity;
	id-unknown sub identity;

# 4.6 Incident
incident sub stix-domain-object, 
	owns name, 
	owns description;

 	# Common Relations
 	# NA

 	# Reverse Relations
 	# NA

	# Embedded relations
	# Creation
	# Object Marking

# 4.7 Indicator
indicator sub stix-domain-object, 
	owns name, 
	owns description, 
	owns indicator-type,
	owns pattern, 
	owns pattern-type, 
	owns pattern-version,
	owns valid-from,
	owns valid-until,

	# Relations defined as properties in STIX
	plays kill-chain-usage:kill-chain-used,

	# Common Relations
	plays indication:indicating,
	plays basing:basing-on,

	# Reverse Relations
	plays investigation:investigated,
	plays mitigation:mitigated;

	# Embedded Relations
	# Creation
	# Object marking ref

# 4.8 Infrastructure 
infrastructure sub stix-domain-object,
	owns name, 
	owns description,
	owns infrastructure-type, # Should this OV be a relation to define?
	owns alias, 
	plays kill-chain-usage:kill-chain-used, 
	owns first-seen,
	owns last-seen,

	# Common Relations
	plays communication:communicating,
	plays consist:consisting,
	plays control:controling,

	plays delivery:delivering,
	plays have:having,
	plays host:hosting,
	plays located-at:locating,
	plays use:used-by,

	# Reverse Relations
	plays control:controled,
	plays communication:communicated,
	plays compromise:compromised,
	plays beacon:beaconed-to,
	plays exfiltrate:exfiltrated-to,
	plays host:hosted,
	# plays indication:indicated, defined in SDO
	plays ownership:owned,
	plays target:targetted,
	plays use:used;

	# Object marking ref

# 4.9 Intrusion Set
intrusion-set sub stix-domain-object,
	owns name, 
	owns description, 
	owns alias, 
	owns first-seen,
	owns last-seen,
	owns goals, 
	owns resource-level, 
	owns primary-motivation, 
	owns secondary-motivations,

	# Common Relations
	plays attribution:attributing,
	plays compromise:compromising,
	plays host:hosting,
	plays ownership:owning,
	plays origination:originating,
	plays target:targetting, 
	plays use:used-by,

	# Reverse Relations
	plays attribution:attributed, 
	plays authorship:authored,
	# plays indication:indicated; Already defined in SDO

	# Embedded Relations
	# Creation
	# Object marking 

	# Inferred role player
	plays inferred-mitigation:mitigated;

# 4.10 Location
location sub stix-domain-object,
	owns name, 
	owns description,
	owns latitude,
	owns longitude,
	owns precision,
	owns region,
	owns country,
	owns administrative-area,
	owns city,
	owns street-address,
	owns postal-code,

	# Common Relations
	# NA

	# Reverse relations
	plays located-at:located,
	plays origination:originated-from,
	plays target:targetted;

	# Embedded Relations
	# Creation
	# Object marking

# 4.11 Malware
malware sub stix-domain-object, 
	owns name, 
	owns description, 
	owns malware-type,
	owns is-family, 
    owns alias,
    owns first-seen,
    owns last-seen,
    owns architecture-execution-envs,
    owns implementation-languages,
    owns capability,
    
    # Relations defined as properties in STIX
	plays kill-chain-usage:kill-chain-used,
	plays execution:executed-malware, # operating_system_refs
    plays malware-sample:associated-to, # ssample_refs

    # Common Relations
    plays authorship:authoring,
    plays beacon:beaconing-to,
    plays exfiltrate:exfiltrating-to,
    plays control:controling,
    plays download:downloading,
    plays drop:dropping,
    plays exploit:exploiting,
    plays origination:originating,
    plays target:targetting,
    plays host:hosting,
    plays use:used-by,
    plays variant:variant-source,

   	# Reverse Relations
   	plays variant:variant-target,
   	plays delivery:delivered,
   	plays mitigation:mitigated,
	plays remediation:remediated,
	plays use:used,
	plays drop:dropped,
	plays control:controled,
	plays characterise:characterised,
	plays av-analysis:analysed,
	plays statis-analysis:analysed,
	plays dynamic-analysis:analysed,

    plays download:downloaded,
    plays exploit:exploiting;

    # Embedded Relations
    # Creation
    # Obejct marking
    # Sample ref


# 4.12 Malware Analysis
malware-analysis sub stix-domain-object,
	owns product, 
	owns version,
	owns configuration-version,
	owns module,
	owns analysis-engine-version,
	owns analysis-definition-version,
	owns submitted,
	owns analysis-started,
	owns analysis-ended,
	owns result-name,
	owns result,

	# Relations defined as properties in STIX
	# host_vm_ref, _operating_system_ref, instsalled_software_ref, analysis-sco are all defined as 
	# playing roles in the analysis relation
	plays malware-analysis-sample:associated-to, # sample_refs

	# Common Relations
	plays characterise:characterising,
	plays av-analysis:analysing,
	plays statis-analysis:analysing,
	plays dynamic-analysis:analysing;

	# Reverse Relations
	# NA

	# Embedded Relations
	# Creation
	# Object markingss
	# host-vm
	# operating-system
	# installed-software
	# analysis-sco
	# sample-ref


# 4.13 Note
note sub stix-domain-object, 
	owns abstract-note, 
	owns content, 
	owns authors,

	# Relations 
	# NA

	# Embedded Relations
	# Creation
	# Object marking
	plays reference:referring;

# 4.14
observed-data sub stix-domain-object,
	owns first-observed, 
	owns last-observed, 
	owns number-observed,

	# Common Relations
	# NA

	# Reverse Relations
	plays basing:based-on,
	plays consist:consisted,

	plays sighting:observed, # This is for SRO 5.2 sighting

	# Embedded Relations
	# Creation
	# Object marking
	plays reference:referring; # Object ref

# 4.15 Opinion
opinion sub stix-domain-object, 
	owns explanation,
	owns authors,
	owns opinion-enum, 

	# Common Relations
	# NA

	# Reverse Relations
	# NA

	# Embedded relations
	# Creation
	# Object marking
	plays reference:referring;

# 4.16 Report
report sub stix-domain-object, 
	owns name, 
	owns description,
	owns report-type, 
	owns published, 

	# Common Relations
	# NA

	# Reverse Relations
	# NA

	# Embedded relations
	# Creation 
	# Object Marking
	plays reference:referring;

# 4.17
threat-actor sub stix-domain-object,
	owns name, 
	owns description, 
	owns threat-actor-type,
	owns alias,
	owns first-seen,
	owns last-seen,
	owns stix-role,
	owns goals,
	owns sophistication,
	owns resource-level, 
	owns primary-motivation,
	owns secondary-motivations,
	owns personal-motivations,


	# Common Relations
	plays attribution:attributing,
	plays compromise:compromising,
	plays host:hosting,
	plays ownership:owning,
	plays impersonate:impersonating,
	plays located-at:locating,
	plays target:targetting,
	plays use:used-by,

	# Reverse Relations
	plays attribution:attributed,
	plays authorship:authored;
	# plays indication:indicated # defined in SDO

	# Embedded relations
	# Creation 
	# Object Marking

# 4.18 Tool
tool sub stix-domain-object,
	owns name,
	owns description,
	owns tool-type,
	owns alias, 
	owns tool-version,

	# Relations defined as properties in STIX
	plays kill-chain-usage:kill-chain-used,

	# Common Relations
	plays delivery:delivering,
	plays drop:dropping,
	plays have:having,
	plays target:targetting,
	plays use:used-by,

	# Reverse Relations
	plays download:downloaded,
	plays drop:dropped,
	plays host:hosted,
	plays mitigation:mitigated,
	plays use:used;
	# plays indication:indicated, Already defined in SDO

	# Embedded Relations
	# Creation
	# Object Marking

# 4.19 Vulnerability TODO
vulnerability sub stix-domain-object,
	owns name, 
	owns description,

	# Relations defined as properties in STIX
	# plays external-referencing:referencing, Defined in SDO

	# Common Relations
	# NA

	# Reverse Relations
 	plays target:targetted,
 	plays exploit:exploited,
 	plays mitigation:mitigated,
 	plays remediation:remediated,
 	plays have:had;

 	#Embedded Relations
 	# Creation
 	# Object Marking

##### 5 STIX™ Relationship Objects #####

# 5.1 Specification-Defined Relatinoships Summary

# 5.2 Sighting
sighting sub stix-core-relationship, 
	owns first-seen,
	owns last-seen,
	owns count,
	owns summary,
	relates saw,
	relates sighting-of, 
	relates observed;

##### 6 STIX™ Cyber-observable Objects ##### 

# 6.1 Artifact Object
artifact sub stix-cyber-observable-object,
	owns mime-type,
	owns payload-bin,
	owns url,
	owns hash-value,
	owns encryption-algorithm,
	owns decryption-key,
	plays network-traffic-source:payload,
	plays network-traffic-destination:payload,
	plays malware-analysis-sample:sco-sample,
	plays malware-sample:sco-sample,
	plays raw-email-reference:binary,
	plays body-raw-reference:non-textual-mime-part,
	plays content-file-containing:containing-file,
	plays message-body-data-reference:contained-data;

# 6.2 Autonomous System Object
autonomous-system sub stix-cyber-observable-object,
	owns number,
	owns name,
	owns rir,
	plays ipv-autonomous-system-belong:belonging-to;

# 6.3 Directory Object
directory sub stix-cyber-observable-object,
	owns path, 
	owns path-enc,
	owns ctime,
	owns mtime,
	owns atime,
	plays directory-reference:parent-directory,
	plays directory-reference:child-directory;

# 6.4 Domain Name Object
domain-name sub stix-cyber-observable-object,
	owns value, 
	plays domain-ipv-resolve:resolving,
	plays domain-ipv-resolve:resolves-to,
	plays communication:communicated,
	plays network-traffic-source:source,
	plays network-traffic-destination:destination;

# 6.5 Email Address Object
email-addr sub stix-cyber-observable-object,
	owns value, 
	owns display-name,
	plays user-email-belong:belonged,
	plays email-sending:from-email,
	plays email-sending:to-email,
	plays email-sending:cc-email,
	plays email-sending:bcc-email;

# 6.6 Email Message
email-message sub stix-cyber-observable-object,
	owns is-multipart,
	owns date,
	owns content-type,
	owns message-id,
	owns subject,
	owns received-lines, # List of type string
	owns body,
	owns additional-header-field-key,
	owns additional-header-field-value,

	plays body-multipart:contained-in,
	plays email-sending:sent-email,
	plays raw-email-reference:email;

	email-mime-part sub stix-cyber-observable-object,
		owns body,
		owns content-type, 
		owns content-disposition, 
		plays body-multipart:mime-part,
		plays body-raw-reference:containing-mime;

# 6.7 File
file sub stix-cyber-observable-object,
	owns size,
	plays hash-association:associated-file, 
	owns size,
	owns name,
	owns name-enc,
	owns magic-number-hex,
	owns mime-type,
	owns ctime,
	owns mtime,
	owns atime, 

	plays directory-reference:file-in-directory,
	plays file-contain:containing-file,
	plays file-contain:contained-file,
	plays content-file-containing:containing-file,
	plays body-raw-reference:non-textual-mime-part,
	plays malware-analysis-sample:sco-sample,
	plays malware-sample:sco-sample,
	plays download:downloaded,
	plays image-process:executed-image,
	plays dll-loading-process:loaded-dll;

	archive-ext sub file,
		owns comment;

	ntfs-ext sub file,
		owns sid, 
		plays ntfs-alt-contain:containing-ntfs-ext;

		alternative-data-stream sub entity, 
			owns name, 
			owns hash-value, 
			owns size, 
			plays ntfs-alt-contain:contained-alt-data-stream; 

	pdf-ext sub file,
		owns version,
		owns is-optimized,
		owns document-info-dict, # TODO 
		owns pdfid0,
		owns pdfid1;

	raster-image-ext sub file,
		owns image-height,
		owns image-width,
		owns bits-per-pixel,
		owns exif-tags; # TODO

	windows-pebinary-ext sub file, 	
		owns pe-type,
		owns imphash,
		owns machine-hex,
		owns number-of-sections,
		owns time-date-stamp,
		owns pointer-to-symbol-table-hex,
		owns number-of-symbols,
		owns size-of-optional-header,
		owns characterstics-hex,
		owns file-header-hash,
		plays optional-header:pebinary,
		plays metadata-specification:pebinary;

		windows-pe-optional-header-type sub entity, 
			owns magic-hex,
			owns major-linker-version,
			owns minor-linker-version,
			owns size-of-code,
			owns size-of-initialized-data,
			owns size-ofuninitialized-data,
			owns address-of-entry-point,
			owns base-of-code,
			owns base-of-data,
			owns section-alignment,
			owns file-alignment,
			owns major-os-version,
			owns minor-os-version,
			owns major-image-version,
			owns minor-image-version,
			owns major-subsystem-version,
			owns minor-subsystem-version,
			owns win32-version-value-hex,
			owns size-of-image,
			owns size-of-headers,
			owns checksum-hex,
			owns subsystem-hex,
			owns dll-characteristics-hex,
			owns size-of-stack-reserve,
			owns size-of-stack-commit,
			owns size-of-heap-reserve,
			owns size-of-heap-commit,
			owns loader-flags-hex,
			owns number-of-rva-and-sizes,
			owns hash-value, 
			plays optional-header:optional-headers;

		windows-pe-section sub entity,
			owns name, 
			owns size, 
			owns entropy, 
			owns hash-value, 
			plays metadata-specification:pe-file-section;

# 6.8 IPv4 Address
ipv4-address sub stix-cyber-observable-object,
	owns value, 
	plays domain-ipv-resolve:resolves-to,
	plays ipv-mac-resolve:resolving,
	plays ipv-autonomous-system-belong:belonged,
	plays communication:communicated,
	plays network-traffic-source:source,
	plays network-traffic-destination:destination;

# 6.9 IPv6 Address
ipv6-address sub stix-cyber-observable-object,
	owns value, 
	plays domain-ipv-resolve:resolves-to,
	plays ipv-mac-resolve:resolving,
	plays ipv-autonomous-system-belong:belonged,	
	plays communication:communicated,
	plays network-traffic-source:source,
	plays network-traffic-destination:destination;

# 6.10 MAC Address
mac-addr sub stix-cyber-observable-object,
	owns value,
	plays ipv-mac-resolve:resolves-to,
	plays network-traffic-source:source,
	plays network-traffic-destination:destination;

# 6.11 Mutex
mutex sub stix-cyber-observable-object,
	owns name; 

# 6.12 Network Traffic - This should really be a relation? 
network-traffic sub stix-cyber-observable-object,
	owns start, 
	owns end,
	owns is-active, 
	owns protocol, 
	owns ipfix, # TODO
	plays network-traffic-source:traffic,
	plays network-traffic-destination:traffic,
	plays network-traffic-encapsulate:encapsulating,
	plays network-traffic-encapsulate:encapsulated,
	plays malware-analysis-sample:sco-sample,
	plays connection-opening:opened-connection;

	# TODO: Should be a hierarchy or a relation? 
	http-request-ext sub network-traffic,
		owns request-method,
		owns request-value,
		owns request-version,
		owns request-header,
		owns message-body-length,
		plays message-body-data-reference:message-body;

	icmp-ext sub network-traffic,
		owns icmp-type-hex,
		owns icmp-code-hex;
	socket-ext sub network-traffic, 
		owns address-family, 
		owns is-blocking,
		owns is-listening,
		owns options, # TODO
		owns socket-type, 
		owns socket-descriptor,
		owns socket-handle;
		
	tcp-ext sub network-traffic,
		owns src-flags-hex,
		owns dst-flags-hex;

# 6.13 Process 
process sub stix-cyber-observable-object,
	owns is-hidden, 
	owns pid,
	owns created-time,
	owns cwd,
	owns command-line,
	owns environment-variables, # TODO

	plays connection-opening:opened-by,
	plays user-creation:process-created,
	plays image-process:executing-process,
	plays process-hierarchy:parent,
	plays process-hierarchy:child;

	windows-process-ext sub process,
		owns aslr-enabled,
		owns dep-enabled,
		owns priority,
		owns owner-sid,
		owns window-title,
		owns startup-info,
		owns integrity-level;

	windows-service-ext sub process, 
		owns service-name,
		owns description,
		owns display-name,
		owns group-name,
		owns start-type,
		owns service-type,
		owns service-status,
		plays dll-loading-process:loading-process;

# 6.14 Software
software sub stix-cyber-observable-object,
	owns name, 
	owns spe, 
	owns swid,
	owns language,
	owns vendor,
	owns version,
	plays analysis:host-vm, 
	plays analysis:operating-system,
	plays analysis:installed-software,
	plays execution:operating-system;

# 6.15 URL
url-object sub stix-cyber-observable-object,
	owns value,
	plays communication:communicated;

# 6.16 User Account
user-account sub stix-cyber-observable-object,
	owns user-id,
	owns credential,
	owns account-login,
	owns account-type,
	owns display-name,
	owns is-service-account,
	owns can-escalate-privs,
	owns is-disabled,
	owns account-created,
	owns account-expires,
	owns credential-last-changed,
	owns account-first-login,
	owns account-last-login,
	plays user-creation:creating,
	plays user-email-belong:belonging-to;

	unix-account-ext sub user-account, 
		owns gid,
		owns unix-group,
		owns home-dir,
		owns shell;

# 6.17 Windows Registry Key
windows-registry-key sub stix-cyber-observable-object, 
	owns attribute-key, 
	owns modified-time, 
	owns number-subkeys, 
	plays user-creation:windows-registry-key-created,
	plays windows-registry-key-value:key;

windows-registry-value-type sub stix-cyber-observable-object, 
	owns name, 
	owns data,
	owns data-type,
	plays windows-registry-key-value:value;

# 6.18 X.509 Certificate
x509-certificate sub stix-cyber-observable-object, 
	owns is-self-signed, 
	owns hash-value, 
	owns version,
	owns serial-number,
	owns signature-algorithm,
	owns issuer,
	owns validity-not-before,
	owns validity-not-after,
	owns subject,
	owns subject-public-key-algorithm,
	owns subject-public-key-modulus,
	owns subject-public-key-exponent;

	x509-v3-extensions sub x509-certificate,
		owns basic-constraints,
		owns name-constraints,
		owns policy-constraints,
		owns key-usage,
		owns extended-key-usage,
		owns subject-key-identifier,
		owns authority-key-identifier,
		owns subject-alternative-name,
		owns issuer-alternative-name,
		owns subject-directory-attributes,
		owns crl-distribution-points,
		owns inhibit-any-policy,
		owns private-key-usage-period-not-before,
		owns private-key-usage-period-not-after,
		owns certificate-policies,
		owns policy-mappings;

##### 7 STIX™ Meta Objects #####

# 7.1 Language Content 

# owns spec-version

# 7.2 Data markings - this describes how data can be used/shared

marking-definition sub stix-meta-object,
	owns name, 
	owns spec-version,
	plays creation:created,
	plays data-marking:marking;

	statement-marking sub marking-definition, 
		owns statement; 

	tlp-marking sub marking-definition;
		tlp-white sub tlp-marking;
		tlp-green sub tlp-marking;
		tlp-amber sub tlp-marking;
		tlp-red sub tlp-marking;


data-marking sub relation, 
	relates marking,
	relates marked;

	object-marking sub data-marking; 

	granular-marking sub data-marking;

# 7.3 Extension Definition


##### RELATIONSHIPS - APENDIX B #####

stix-core-relationship sub relation,
	# Required
	owns spec-version,
	owns stix-id, 
	owns created,
	owns modified,
	owns stix-type,

	# Optional
	owns description,
	owns revoked,
	owns labels,
	owns confidence, 
	owns langs,
	owns extensions, 

	owns custom-attribute,

	relates active-role, 
	relates passive-role,
	plays object-marking:marked, 
	plays external-referencing:referencing;


	delivery sub stix-core-relationship, 
		relates delivering, 
		relates delivered;

	target sub stix-core-relationship, 
		relates targetting,
		relates targetted;

	attribution sub stix-core-relationship, 
		relates attributing,
		relates attributed; 

	mitigation sub stix-core-relationship,
		relates mitigated,
		relates mitigating;

	indication sub stix-core-relationship, 
		relates indicating,
		relates indicated; 

	creation sub stix-core-relationship, 
		relates created,
		relates creator;

	reference sub stix-core-relationship, 
		relates referred,
		relates referring;

	use sub stix-core-relationship,
		relates used-by,
		relates used;

	located-at sub stix-core-relationship,
		relates located,
		relates locating;

	origination sub stix-core-relationship,
		relates originated-from,
		relates originating;

	have sub stix-core-relationship, # Could come up with a better name?
		relates having,
		relates had;

	host sub stix-core-relationship,
		relates hosting,
		relates hosted;

	ownership sub stix-core-relationship,
		relates owned,
		relates owning;

	compromise sub stix-core-relationship,
		relates compromising,
		relates compromised;

	authorship sub stix-core-relationship,
		relates authoring,
		relates authored;

	drop sub stix-core-relationship,
		relates dropped,
		relates dropping;
	
	download sub stix-core-relationship,
		relates downloaded,
		relates downloading;

	exploit sub stix-core-relationship,
		relates exploiting,
		relates exploited;

	investigation sub stix-core-relationship,
		relates investigating,
		relates investigated;

	remediation sub stix-core-relationship,
		relates remediating,
		relates remediated;

	basing sub stix-core-relationship,
		relates basing-on,
		relates based-on;

	communication sub stix-core-relationship,
		relates communicating,
		relates communicated;

	consist sub stix-core-relationship,
		relates consisting,
		relates consisted;

	control sub stix-core-relationship,
		relates controling,
		relates controled;

	beacon sub stix-core-relationship,
		relates beaconed-to,
		relates beaconing-to;

	exfiltrate sub stix-core-relationship,
		relates exfiltrated-to,
		relates exfiltrating-to;

	variant sub stix-core-relationship,
		relates variant-source,
		relates variant-target;

	characterise sub stix-core-relationship,
		relates characterised,
		relates characterising;

	analysis sub stix-core-relationship,
		relates analysing,
		relates analysed,

		relates host-vm, 
		relates operating-system,
		relates installed-software,
		relates captured-sco;

		av-analysis sub analysis;
		statis-analysis sub analysis;
		dynamic-analysis sub analysis;

	impersonate sub stix-core-relationship,
		relates impersonating,
		relates impersonated;

	resolve sub stix-core-relationship,
		relates resolving,
		relates resolves-to;

	ipv-mac-resolve sub resolve;
	domain-ipv-resolve sub resolve;

# Relations that are defined as properties in STIX
sample sub relation, 
	relates associated-to, 
	relates sco-sample;

	malware-sample sub sample;
	malware-analysis-sample sub sample;

execution sub relation,
	relates host-vm,
	relates operating-system, 
	relates executed-malware;

hash-association sub relation,
	relates contained-hash,
	relates associated-file;

os-host sub relation,
	relates installed-software,
	relates os-system;

directory-reference sub relation,
	relates child-directory, 
	relates parent-directory,
	relates file-in-directory;

belong sub relation,
	relates belonged,
	relates belonging-to;

	ipv-autonomous-system-belong sub belong;
	user-email-belong sub belong;

email-sending sub relation, 
	relates from-email,
	relates sender-email,
	relates to-email,
	relates cc-email,
	relates bcc-email, 
	relates sent-email;

raw-email-reference sub relation, 
	relates email,
	relates binary;

body-raw-reference sub relation,
	relates containing-mime,
	relates non-textual-mime-part;

body-multipart sub relation, 
	relates contained-in,
	relates mime-part;

file-contain sub relation, 
	relates contained-coo,
	relates contained-file,
	relates containing-file;

content-file-containing sub relation, 
	relates containing-file,
	relates contained-content;

ntfs-alt-contain sub relation,
	relates containing-ntfs-ext,
	relates contained-alt-data-stream;

optional-header sub relation, 
	relates pebinary,
	relates optional-headers;

metadata-specification sub relation, 
	relates pe-file-section,
	relates pebinary;

network-traffic-source sub relation, 
	relates traffic,
	relates source, 
	relates payload, 
	owns src-port,
	owns src-byte-count,
	owns src-packets;

network-traffic-destination sub relation, 
	relates traffic,
	relates destination,
	relates payload,
	owns dst-port, 
	owns dst-byte-count,
	owns dst-packets; 

network-traffic-encapsulate sub relation,
	relates encapsulating,
	relates encapsulated;

message-body-data-reference sub relation,
	relates contained-data,
	relates message-body;

connection-opening sub relation,
	relates opened-connection,
	relates opened-by;

user-creation sub relation,
	relates windows-registry-key-created,
	relates process-created,
	relates creating;

image-process sub relation,	
	relates executed-image,
	relates executing-process;

process-hierarchy sub relation,
	relates parent,
	relates child;

dll-loading-process sub relation, 
	relates loaded-dll,
	relates loading-process;

windows-registry-key-value sub relation,
	relates key,
	relates value;

# 3.7 Common Relationships 
# These are relation types that are shared by SDOs and SCOs

derivation sub relation, 
	relates derived-from,
	relates deriving;

duplicate sub relation, 
	relates duplicated-object; 

relatedness sub relation,
	relates related-to; 

## INFERRED RELATIONS ## 

kill-chain sub relation, 
	owns kill-chain-name, 
	relates participating-kill-chain-phase; 

inferred-mitigation sub mitigation;

##### ATTRIBUTES #####

stix-attribute-string sub attribute, value string, abstract,
	plays granular-marking:marked;

	stix-type sub stix-attribute-string;
	stix-id sub stix-attribute-string;
	object-marking-refs sub stix-attribute-string;
	created sub stix-attribute-string;
	modified sub stix-attribute-string;
	labels sub stix-attribute-string;
	confidence sub stix-attribute-string;
	langs sub stix-attribute-string;
	defanged sub stix-attribute-string;
	extensions sub stix-attribute-string;
	source-name sub stix-attribute-string;
	description sub stix-attribute-string;
	url sub stix-attribute-string;
	external-id sub stix-attribute-string;

	kill-chain-name sub stix-attribute-string; 
	phase-name sub stix-attribute-string; 
	name sub stix-attribute-string; 
	sector sub stix-attribute-string; 
	contact-information sub stix-attribute-string; 
	indicator-type sub stix-attribute-string; 
	pattern sub stix-attribute-string; 
	pattern-type sub stix-attribute-string; 
	pattern-version sub stix-attribute-string; 
	valid-from sub stix-attribute-string; 
	valid-until sub stix-attribute-string; 

	malware-type sub stix-attribute-string; 
	first-seen sub stix-attribute-string; 
	last-seen sub stix-attribute-string; 
	operating-system-refs sub stix-attribute-string; 
	architecture-execution-envs sub stix-attribute-string; 
	implementation-languages sub stix-attribute-string; 
	capability sub stix-attribute-string; 
	sample-refs sub stix-attribute-string; 

	first-observed sub stix-attribute-string; 
	last-observed sub stix-attribute-string; 
	object sub stix-attribute-string; 

	threat-actor-type sub stix-attribute-string; 
	alias sub stix-attribute-string; 
	first-seen sub stix-attribute-string; 
	last-seen sub stix-attribute-string; 
	stix-role sub stix-attribute-string; 
	goals sub stix-attribute-string; 
	sophistication sub stix-attribute-string; 
	resource-level sub stix-attribute-string; 
	primary-motivation sub stix-attribute-string; 
	secondary-motivations sub stix-attribute-string; 
	personal-motivations sub stix-attribute-string; 

	first-seen sub stix-attribute-string; 
	last-seen sub stix-attribute-string; 
	summary sub stix-attribute-string; 
	spec-version sub stix-attribute-string; 
	attribute-key sub stix-attribute-string; 
	value sub stix-attribute-string; 
	modified-time sub stix-attribute-string; 
	number-subkeys sub stix-attribute-string; 
	objective sub stix-attribute-string;
	statement sub stix-attribute-string;
	action sub stix-attribute-string;  
	tool-type sub stix-attribute-string; # OPEN VOCAB?
	tool-version sub stix-attribute-string; 

	region sub stix-attribute-string; # open vocab TODO (10.21 Region Vocabulary)
	country sub stix-attribute-string; 
	administrative-area sub stix-attribute-string; 
	city sub stix-attribute-string; 
	street-address sub stix-attribute-string; 
	postal-code sub stix-attribute-string; 
	infrastructure-type sub stix-attribute-string;
	context sub stix-attribute-string;

	product sub stix-attribute-string;
	version sub stix-attribute-string;
	configuration-version sub stix-attribute-string;
	module sub stix-attribute-string;
	analysis-engine-version sub stix-attribute-string;
	analysis-definition-version sub stix-attribute-string;
	result-name sub stix-attribute-string;
	result sub stix-attribute-string;

	abstract-note sub stix-attribute-string;
	content sub stix-attribute-string;
	report-type sub stix-attribute-string;
	explanation sub stix-attribute-string;
	authors sub stix-attribute-string;
	opinion-enum sub stix-attribute-string;
	hash-value sub stix-attribute-string;
	imphash sub stix-attribute-string;
	hash-general sub stix-attribute-string;
	magic-number-hex sub stix-attribute-string;
	pe-type sub stix-attribute-string;
	name-enc sub stix-attribute-string;
	mime-type sub stix-attribute-string;
	payload-bin sub stix-attribute-string;
	encryption-algorithm sub stix-attribute-string;
	decryption-key sub stix-attribute-string;
	rir sub stix-attribute-string;
	path sub stix-attribute-string;
	path-enc sub stix-attribute-string;

	display-name sub stix-attribute-string;
	content-type sub stix-attribute-string;
	message-id sub stix-attribute-string;
	subject sub stix-attribute-string;
	received-lines sub stix-attribute-string;
	body sub stix-attribute-string;
	additional-header-field-key sub stix-attribute-string;
	additional-header-field-value sub stix-attribute-string;

	content-disposition sub stix-attribute-string;
	sid sub stix-attribute-string;
	is-optimized sub stix-attribute-string;
	pdfid0 sub stix-attribute-string;
	pdfid1 sub stix-attribute-string;
	document-info-dict sub stix-attribute-string;
	exif-tags sub stix-attribute-string;

	machine-hex sub stix-attribute-string;
	pointer-to-symbol-table-hex sub stix-attribute-string;
	characterstics-hex sub stix-attribute-string;
	file-header-hash sub stix-attribute-string;
	loader-flags-hex sub stix-attribute-string;
	dll-characteristics-hex sub stix-attribute-string;
	win32-version-value-hex sub stix-attribute-string;
	magic-hex sub stix-attribute-string;
	protocol sub stix-attribute-string;
	ipfix sub stix-attribute-string;

	icmp-type-hex sub stix-attribute-string;
	icmp-code-hex sub stix-attribute-string;
	request-method sub stix-attribute-string;
	request-value sub stix-attribute-string;
	request-version sub stix-attribute-string;
	request-header sub stix-attribute-string;
	address-family sub stix-attribute-string;
	options sub stix-attribute-string;
	socket-type sub stix-attribute-string;
	src-flags-hex sub stix-attribute-string;
	dst-flags-hex sub stix-attribute-string;

	checksum-hex sub stix-attribute-string;
	cwd sub stix-attribute-string;
	command-line sub stix-attribute-string;
	environment-variables sub stix-attribute-string;
	priority sub stix-attribute-string;
	owner-sid sub stix-attribute-string;
	window-title sub stix-attribute-string;
	startup-info sub stix-attribute-string;
	integrity-level sub stix-attribute-string;
	service-name sub stix-attribute-string;
	display-name sub stix-attribute-string;
	group-name sub stix-attribute-string;
	start-type sub stix-attribute-string;
	service-type sub stix-attribute-string;
	service-status sub stix-attribute-string;

	spe sub stix-attribute-string;
	swid sub stix-attribute-string;
	language sub stix-attribute-string;
	vendor sub stix-attribute-string;
	user-id sub stix-attribute-string;
	credential sub stix-attribute-string;
	account-login sub stix-attribute-string;
	account-type sub stix-attribute-string;
	display-name sub stix-attribute-string;

	gid sub stix-attribute-string;
	unix-group sub stix-attribute-string;
	home-dir sub stix-attribute-string;
	shell sub stix-attribute-string;
	data-type sub stix-attribute-string;
	serial-number sub stix-attribute-string;
	signature-algorithm sub stix-attribute-string;
	issuer sub stix-attribute-string;
	subject-public-key-algorithm sub stix-attribute-string;
	subject-public-key-modulus sub stix-attribute-string;

	basic-constraints sub stix-attribute-string;
	name-constraints sub stix-attribute-string;
	policy-constraints sub stix-attribute-string;
	key-usage sub stix-attribute-string;
	extended-key-usage sub stix-attribute-string;
	subject-key-identifier sub stix-attribute-string;
	authority-key-identifier sub stix-attribute-string;
	subject-alternative-name sub stix-attribute-string;
	issuer-alternative-name sub stix-attribute-string;
	subject-directory-attributes sub stix-attribute-string;
	crl-distribution-points sub stix-attribute-string;
	inhibit-any-policy sub stix-attribute-string;
	certificate-policies sub stix-attribute-string;
	policy-mappings sub stix-attribute-string;
	data sub stix-attribute-string;
	comment sub stix-attribute-string;

stix-attribute-double sub attribute, value double, abstract,
	plays granular-marking:marked;		

	latitude sub stix-attribute-double;
	longitude sub stix-attribute-double;
	precision sub stix-attribute-double;
	image-height sub stix-attribute-double;
	image-width sub stix-attribute-double;
	bits-per-pixel sub stix-attribute-double;
	number-of-sections sub stix-attribute-double;
	number-of-symbols sub stix-attribute-double;
	size-of-optional-header sub stix-attribute-double;
	size sub stix-attribute-double; 
	count sub stix-attribute-double; 
	number-observed sub stix-attribute-double; 
	number sub stix-attribute-double;
	major-linker-version sub stix-attribute-double;
	minor-linker-version sub stix-attribute-double;
	size-of-code sub stix-attribute-double;
	size-of-initialized-data sub stix-attribute-double;
	size-ofuninitialized-data sub stix-attribute-double;
	address-of-entry-point sub stix-attribute-double;
	base-of-code sub stix-attribute-double;
	base-of-data sub stix-attribute-double;
	section-alignment sub stix-attribute-double;
	file-alignment sub stix-attribute-double;
	major-os-version sub stix-attribute-double;
	minor-os-version sub stix-attribute-double;
	major-image-version sub stix-attribute-double;
	minor-image-version sub stix-attribute-double;
	major-subsystem-version sub stix-attribute-double;
	minor-subsystem-version sub stix-attribute-double;
	size-of-image sub stix-attribute-double;
	size-of-headers sub stix-attribute-double;
	subsystem-hex sub stix-attribute-double;	
	size-of-stack-reserve sub stix-attribute-double;
	size-of-stack-commit sub stix-attribute-double;
	size-of-heap-reserve sub stix-attribute-double;
	size-of-heap-commit sub stix-attribute-double;
	number-of-rva-and-sizes sub stix-attribute-double;
	entropy sub stix-attribute-double;
	src-port sub stix-attribute-double;
	dst-port sub stix-attribute-double;
	src-byte-count sub stix-attribute-double;
	dst-byte-count sub stix-attribute-double;
	dst-packets sub stix-attribute-double;
	src-packets sub stix-attribute-double;
	dst-packet sub stix-attribute-double;
	message-body-length sub stix-attribute-double;
	socket-descriptor sub stix-attribute-double;
	socket-handle sub stix-attribute-double;
	pid sub stix-attribute-double;
	subject-public-key-exponent sub stix-attribute-double;

stix-attribute-boolean sub attribute, value boolean, abstract, 
	plays granular-marking:marked;

	revoked sub stix-attribute-boolean;
	is-family sub stix-attribute-boolean;
	is-multipart sub stix-attribute-boolean;
	is-active sub stix-attribute-boolean;
	is-blocking sub stix-attribute-boolean;
	is-listening sub stix-attribute-boolean;
	is-hidden sub stix-attribute-boolean;
	aslr-enabled sub stix-attribute-boolean;
	dep-enabled sub stix-attribute-boolean;
	is-service-account sub stix-attribute-boolean;
	is-privileged sub stix-attribute-boolean;
	can-escalate-privs sub stix-attribute-boolean;
	is-disabled sub stix-attribute-boolean;
	is-self-signed sub stix-attribute-boolean;

stix-attribute-timestamp sub attribute, value datetime, abstract, 
	plays granular-marking:marked;

	date sub stix-attribute-timestamp;
	submitted sub stix-attribute-timestamp;
	analysis-started sub stix-attribute-timestamp;
	analysis-ended sub stix-attribute-timestamp;
	published sub stix-attribute-timestamp;
	ctime sub stix-attribute-timestamp;
	mtime sub stix-attribute-timestamp;
	atime sub stix-attribute-timestamp;
	time-date-stamp sub stix-attribute-timestamp;
	start sub stix-attribute-timestamp;
	end sub stix-attribute-timestamp;
	created-time sub stix-attribute-timestamp;
	account-created sub stix-attribute-timestamp;
	account-expires sub stix-attribute-timestamp;
	credential-last-changed sub stix-attribute-timestamp;
	account-first-login sub stix-attribute-timestamp;
	account-last-login sub stix-attribute-timestamp;
	validity-not-before sub stix-attribute-timestamp;
	validity-not-after sub stix-attribute-timestamp;
	private-key-usage-period-not-before sub stix-attribute-timestamp;
	private-key-usage-period-not-after sub stix-attribute-timestamp;

custom-attribute sub attribute, value string, 
	plays granular-marking:marked,
	owns attribute-type; 

attribute-type sub attribute, value string;

##### Rules #####

# Description
# These two rules create a relation of type kill-chain between all the kill-chain-phase entities
# that have the same name. The first rule infers the relation, and the second rule infers the attribute 
# kill-chain-name. We need to split this up into two rules as we can only infer one relation or one attribute 
# per rule. 
# Example: 
# If we have many different kill-chain-phase entites that are called "Mitre Attack", but with different phases, 
# we can infer a kill-chain relation between all the phases in that kill chain. This makes it easy for us to 
# query across all the phases in that kill chain. 
rule part-of-one-kill-chain-relation:
when {
	$kill-chain-1 isa kill-chain-phase, has kill-chain-name $x;
	$kill-chain-2 isa kill-chain-phase, has kill-chain-name $x;
	not {$kill-chain-1 is $kill-chain-2;};
} then {
	(participating-kill-chain-phase: $kill-chain-1, participating-kill-chain-phase: $kill-chain-2) isa kill-chain;
};

rule part-of-one-kill-chain-attribute-name:
when {
	$kill-chain-1 isa kill-chain-phase, has kill-chain-name $x;
	$kill-chain-2 isa kill-chain-phase, has kill-chain-name $x;
	not {$kill-chain-1 is $kill-chain-2;};
	$kill-chain (participating-kill-chain-phase: $kill-chain-1, participating-kill-chain-phase: $kill-chain-2) isa kill-chain;
} then {
	$kill-chain has $x;
};

# Description
# This rule infers that if X is using Y, and if Y is using Z, then that means that X is also using Z
# Example: 
# If an intrusion set is using a malware, and if that malware is using a specific attack pattern, 
# then we want to infer that that intrusion set is using that specific attack pattern 
rule transitive-use:
when {
	$x isa stix-domain-object, has name $name1;
	$y isa stix-domain-object, has name $name2;
	$z isa stix-domain-object, has name $name3;
	$use1 (used-by: $x, used: $y) isa use;
	$use2 (used-by: $y, used: $z) isa use;
} then {
	(used-by: $x, used: $z) isa use;
};

# Description: 
# If y has been attributed to x, and if y is using z, then we infer that x is using z also
# Example: 
# If an identity has been attributed to a threat actor, 
# and if that identity is using a malware, then we want to create a "use" relation
# between the threat actor and the malware; the threat actor is using the malware 
# through the identiy
rule attribution-when-using:
when {
	(attributing: $x, attributed: $y) isa attribution; 
	(used-by: $y, used: $z) isa use; 
} then {
	(used-by: $x, used: $z) isa use;
};

# Description: 
# If y has been attributed to x, and if y is targetting z, then we can infer that x is targetting z
# Example: 
# If an attack pattern, for example "spear fishing", has been attributed to a specific identity named "Bravo",
# and if that attack pattern is targetting a location, for example New York, 
# we can infer that the identiy "Bravo" is targeting the location New York
rule attribution-when-targeting:
when {
	(attributing: $x, attributed: $y) isa attribution; 
	(targetting: $y, targetted: $z) isa target; 
} then {
	(targetting: $x, targetted: $z) isa target;
};

# Description
# This rule infers a relation of type inferred-mitigation between a course of action and an intursion set, if the 
# course of action mitigates an SDO which is used by an intrusion set. Note: as the relation "use" is transitive (see rule above),
# the SDO and the intrusion set may not be directly connected. 
# Example:
# The entity type "course-of-action" with name "Restrict File and Directory Permissions" is mitigating against an entity "attack-pattern" 
# with name "indicator Blocking". If that "attack-pattern" is being used by an entiy of type "intrusion-set" with name "BlackTech", then an i
# nferred relation of type "inferred-mitigation" will be created between the "course-of-action" and "the intrusion-set"
rule mitigating-course-of-action-with-intrusion-set:
when {
	$course-of-action isa course-of-action, has name $name1;
	$sdo isa stix-domain-object, has name $name2;
	$intrusion-set isa intrusion-set, has name $name3;
	$mitigation (mitigating: $course-of-action, mitigated: $sdo) isa mitigation;
	$use (used: $sdo, used-by: $intrusion-set) isa use; 
	
} then {
	(mitigating: $course-of-action, mitigated: $intrusion-set) isa inferred-mitigation;
};